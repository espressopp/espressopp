if(USE_GCOV)
  set(PY_COV_OPTS "-m" "coverage" "run" "-a")
endif()

file(GLOB ALL_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/*)
foreach(DIR ${ALL_DIRS})
    if(IS_DIRECTORY ${DIR})
        if (EXISTS "${DIR}/IGNORE")
            continue()
        endif()
        if (EXISTS "${DIR}/CMakeLists.txt")
            add_subdirectory(${DIR})
        else()
            file(GLOB_RECURSE TESTS "${DIR}/*Test*.cpp")
            add_definitions(-DBOOST_TEST_DYN_LINK)
            foreach(TEST_FILE ${TESTS})
                get_filename_component(TEST_NAME ${TEST_FILE} NAME_WE)
                get_filename_component(TEST_DIR ${TEST_FILE} DIRECTORY)
                if (EXISTS "${TEST_DIR}/IGNORE")
                    continue()
                endif()
                add_executable(${TEST_NAME} ${TEST_FILE})
                add_test(${TEST_NAME} ${CMAKE_CURRENT_BINARY_DIR}/${TEST_NAME})
                set_tests_properties(${TEST_NAME} PROPERTIES ENVIRONMENT "${ESP_PY_ENV}")
                target_link_libraries(${TEST_NAME} _espressopp Boost::unit_test_framework)
            endforeach()

            file(GLOB_RECURSE TESTS_PY "${DIR}/test*.py")
            foreach(TEST_FILE ${TESTS_PY})
                get_filename_component(TEST_NAME ${TEST_FILE} NAME_WE)
                get_filename_component(TEST_DIR ${TEST_FILE} DIRECTORY)
                if (EXISTS "${TEST_DIR}/IGNORE")
                    continue()
                endif()
                add_test(${TEST_NAME} ${Python3_EXECUTABLE} ${PY_COV_OPTS} ${TEST_FILE})
                set_tests_properties(${TEST_NAME} PROPERTIES ENVIRONMENT "${ESP_PY_ENV}")
            endforeach()
        endif()
    endif()
endforeach()
